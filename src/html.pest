file = _{
    SOI ~ comment* ~ doctype? ~ node* ~ EOI
}

node = _{ (comment | node_element | element_text) }

doctype = { chev_op_bang ~ ^"DOCTYPE" ~ attribute* ~ chev_cl }

node_element = { (element_closable | element_norm) }

element_closable = { chev_op_norm ~ word ~ attribute* ~ chev_cl_end }

element_norm       = { element_norm_start ~ (!element_norm_end ~ node)* ~ element_norm_end }
element_norm_end   = { chev_op_norm ~ POP ~ chev_cl_end }
element_norm_start = { chev_op_norm ~ PUSH(word) ~ attribute* ~ chev_cl }

element_text = { (!(comment_op | chev_op_norm | chev_op_bang) ~ ANY)* }

comment    =  { comment_op ~ comment_bd ~ comment_cl }
comment_bd =  { (!comment_cl ~ ANY)* }
comment_cl = _{ "--" ~ chev_cl }
comment_op = _{ chev_op_bang ~ "--" }

attribute = ${ attr_key ~ ("=" ~ attr_val)? }
attr_key  =  { word }
attr_val  =  { (word | sentence_delim) }

sentence_delim = _{ PUSH(quote) ~ wordnum+ ~ POP }

quote = _{ ("\"" | "'") }

text    = _{ (ASCII_ALPHANUMERIC | "_" | "-" | ":" | " ") }
wordnum =  { text+ }
word    =  { ASCII_ALPHA+ }

chev_op_bang = _{ "<!" }
chev_op_norm = _{ "<" }
chev_cl      = _{ ">" | chev_cl_end | "?>" }
chev_cl_end  =  { "/>" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
